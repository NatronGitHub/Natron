# Maintainer: Alexey Pavlov <Alexpux@gmail.com>
# Contributer: Zach Bacon <11doctorwhocanada@gmail.com>
# Contributer: wirx6 <wirx654@gmail.com>

_realname=ffmpeg
pkgbase="mingw-w64-${_realname}-gpl2"
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}-gpl2"
pkgver=4.4.1
pkgrel=1
pkgdesc="Complete and free Internet live audio and video broadcasting solution (mingw-w64)"
arch=('any')
url="https://ffmpeg.org/"
license=('GPL')
options=('staticlibs' 'strip')
depends=("${MINGW_PACKAGE_PREFIX}-bzip2"
         "${MINGW_PACKAGE_PREFIX}-celt"
         "${MINGW_PACKAGE_PREFIX}-fontconfig"
         "${MINGW_PACKAGE_PREFIX}-gnutls"
         "${MINGW_PACKAGE_PREFIX}-gsm"
         "${MINGW_PACKAGE_PREFIX}-lame"
         "${MINGW_PACKAGE_PREFIX}-libass"
         "${MINGW_PACKAGE_PREFIX}-libbluray"
         "${MINGW_PACKAGE_PREFIX}-libcaca"
         "${MINGW_PACKAGE_PREFIX}-libmfx"
         "${MINGW_PACKAGE_PREFIX}-libmodplug"
	 "${MINGW_PACKAGE_PREFIX}-librsvg"
         "${MINGW_PACKAGE_PREFIX}-libtheora"
         "${MINGW_PACKAGE_PREFIX}-libvorbis"
         "${MINGW_PACKAGE_PREFIX}-libvpx"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-openal"
	 "${MINGW_PACKAGE_PREFIX}-openh264"
         "${MINGW_PACKAGE_PREFIX}-openjpeg2"
         "${MINGW_PACKAGE_PREFIX}-opus"
         "${MINGW_PACKAGE_PREFIX}-rtmpdump"
         #"${MINGW_PACKAGE_PREFIX}-SDL2"
	 "${MINGW_PACKAGE_PREFIX}-snappy"
         "${MINGW_PACKAGE_PREFIX}-speex"
         "${MINGW_PACKAGE_PREFIX}-srt"
         #"${MINGW_PACKAGE_PREFIX}-wavpack"
         "${MINGW_PACKAGE_PREFIX}-x264"
         "${MINGW_PACKAGE_PREFIX}-x265"
         "${MINGW_PACKAGE_PREFIX}-xvidcore"
	 "${MINGW_PACKAGE_PREFIX}-dav1d"
	 "${MINGW_PACKAGE_PREFIX}-aom"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-gcc"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
	     "${MINGW_PACKAGE_PREFIX}-nasm")
source=(https://ffmpeg.org/releases/${_realname}-${pkgver}.tar.xz)
sha256sums=('eadbad9e9ab30b25f5520fbfde99fae4a92a1ae3c0257a8d68569a4651e30e02')

prepare() {
  cd "${srcdir}/${_realname}-${pkgver}"
}

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p "${srcdir}/build-${MINGW_CHOST}"
  cd "${srcdir}/build-${MINGW_CHOST}"

  # Fix using SRT headers
  CFLAGS+=" -DWIN32"
  CXXFLAGS+=" -DWIN32"

  CC=${MINGW_PREFIX}/bin/gcc \
  CXX=${MINGW_PREFIX}/bin/g++ \
  ../${_realname}-${pkgver}/configure \
    --prefix=${MINGW_PREFIX}/ffmpeg-gpl2 \
    --target-os=mingw32 \
    --arch=${CARCH%%-*} \
    --disable-debug \
    --disable-static \
    --enable-dxva2 \
    --enable-d3d11va \
    --enable-fontconfig \
    --enable-gnutls \
    --enable-gpl \
    --enable-libdav1d \
    --enable-libaom \
    --enable-libass \
    --enable-libbluray \
    --enable-libcaca \
    --enable-libcelt \
    --enable-libfreetype \
    --enable-libgsm \
    --enable-libmfx \
    --enable-libmodplug \
    --enable-libmp3lame \
    --enable-libopenh264 \
    --enable-libopenjpeg \
    --enable-libopus \
    --enable-librsvg \
    --enable-librtmp \
    --enable-libsnappy \
    --enable-libspeex \
    --enable-libsrt \
    --enable-libtheora \
    --enable-libvorbis \
    --enable-libx264 \
    --enable-libx265 \
    --enable-libxvid \
    --enable-libvpx \
    --enable-libwebp \
    --enable-openal \
    --enable-pic \
    --enable-postproc \
    --enable-runtime-cpudetect \
    --enable-shared \
    --enable-static \
    --enable-swresample \
    --enable-zlib \
    --disable-doc
    #--enable-pthreads

  make
}

check() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  # workaround for conflict with SDL main(), use it if you have SDL installed
  #make check CC_C="-c -Umain"
  #make check
}

package() {
  cd "${srcdir}/build-${MINGW_CHOST}"
  make DESTDIR="${pkgdir}" install
  mv "${pkgdir}${MINGW_PREFIX}/ffmpeg-gpl2"/bin/*.lib "${pkgdir}${MINGW_PREFIX}/ffmpeg-gpl2"/lib/

  #find ${pkgdir}${MINGW_PREFIX}/ffmpeg-gpl2/bin -type f -name "*.exe" -exec objcopy --subsystem console {} \;
  rm -f ${pkgdir}${MINGW_PREFIX}/ffmpeg-gpl2/lib/*.def
  rm -f ${pkgdir}${MINGW_PREFIX}/ffmpeg-gpl2/lib/*.lib
  
  local PREFIX_DEPS=$(cygpath -am ${MINGW_PREFIX})
  find ${pkgdir}${MINGW_PREFIX}/ffmpeg-gpl2/lib/pkgconfig -name *.pc -exec sed -i -e"s|${PREFIX_DEPS}|${MINGW_PREFIX}/ffmpeg-gpl2|g" {} \;
}
