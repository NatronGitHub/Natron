# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openexr
pkgbase=mingw-w64-natron_${_realname}
pkgname=("${MINGW_PACKAGE_PREFIX}-natron_${_realname}")
pkgver=3.2.3
pkgrel=99.1
pkgdesc='A high dynamic-range image file format library (mingw-w64)'
arch=('any')
mingw_arch=('mingw32' 'mingw64' 'ucrt64' 'clang64' 'clang32' 'clangarm64')
url="https://www.openexr.com/"
license=('spdx:BSD-3-Clause')
depends=("${MINGW_PACKAGE_PREFIX}-natron_imath"
         "${MINGW_PACKAGE_PREFIX}-libdeflate"
         "${MINGW_PACKAGE_PREFIX}-libwinpthread-git")
makedepends=("${MINGW_PACKAGE_PREFIX}-cc"
             "${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-ninja")
conflicts=(
  "${MINGW_PACKAGE_PREFIX}-ilmbase"
  "${MINGW_PACKAGE_PREFIX}-pyilmbase"
  "${MINGW_PACKAGE_PREFIX}-${_realname}"
)
replaces=(
  "${MINGW_PACKAGE_PREFIX}-ilmbase"
  "${MINGW_PACKAGE_PREFIX}-pyilmbase"
)
source=("https://github.com/openexr/openexr/archive/v${pkgver}/${_realname}-${pkgver}.tar.gz"
        0006-cmake-soversion.patch
        0007-export-TypedAttribute-value.patch)
sha256sums=('f3f6c4165694d5c09e478a791eae69847cadb1333a2948ca222aa09f145eba63'
            '5653da878ebbf5f3087bb529213931b48e151aef082b34e19d189e22be0411e7'
            '204777b48cde1c4d390789f10c5e429d5295be701f68a7937a90d0ecf271c048')

# Helper macros to help make tasks easier #
apply_patch_with_msg() {
  for _patch in "$@"
  do
    msg2 "Applying $_patch"
    patch -Np1 -i "${srcdir}/$_patch"
  done
}

prepare(){
  cd "${srcdir}/${_realname}-${pkgver}"

  apply_patch_with_msg \
    0006-cmake-soversion.patch \
    0007-export-TypedAttribute-value.patch
}

build() {
  mkdir -p "${srcdir}/build-${MSYSTEM}" && cd "${srcdir}/build-${MSYSTEM}"

  declare -a extra_config
  if check_option "debug" "n"; then
    extra_config+=("-DCMAKE_BUILD_TYPE=Release")
  else
    extra_config+=("-DCMAKE_BUILD_TYPE=Debug")
  fi

  if [[ ${MINGW_PACKAGE_PREFIX} == *-clang-* ]]; then
    extra_config+=("-DOPENEXR_BUILD_TOOLS=OFF")
  fi

  CXXFLAGS+=" -Wno-ignored-attributes"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
    ${MINGW_PREFIX}/bin/cmake \
      -GNinja \
      -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
      ${extra_config[@]} \
      -DBUILD_SHARED_LIBS=ON \
      -DBUILD_TESTING=OFF \
      -DOPENEXR_BUILD_EXAMPLES=OFF \
      ../${_realname}-${pkgver}

  ${MINGW_PREFIX}/bin/cmake --build .
}

#check() {
#  cd "${srcdir}/build-${MSYSTEM}"
#  ${MINGW_PREFIX}/bin/cmake -DBUILD_TESTING=ON ../${_realname}-${pkgver}
#  ${MINGW_PREFIX}/bin/cmake --build .
#  ${MINGW_PREFIX}/bin/ctest.exe ./ || true
#}

package() {
  cd "${srcdir}/build-${MSYSTEM}"
  DESTDIR="${pkgdir}" ${MINGW_PREFIX}/bin/cmake.exe --install ./

  install -Dm644 "${srcdir}/${_realname}-${pkgver}/LICENSE.md" "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE.md"
}
