# Maintainer: ole.andre.rodlie@gmail.com

_realname=opencolorio
pkgbase=mingw-w64-natron_${_realname}-git
pkgname="${MINGW_PACKAGE_PREFIX}-natron_${_realname}-git"
gitcommit=66b7b7d # 2.1.3
pkgver=1356.66b7b7d5
pkgrel=99.1
pkgdesc="A color management framework for visual effects and animation (mingw-w64)"
arch=('any')
url="http://opencolorio.org"
license=('BSD')
provides=("${MINGW_PACKAGE_PREFIX}-${_realname}")
conflicts=("${MINGW_PACKAGE_PREFIX}-${_realname}")
depends=("${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-lcms2"
         "${MINGW_PACKAGE_PREFIX}-tinyxml"
         "${MINGW_PACKAGE_PREFIX}-yaml-cpp"
         "${MINGW_PACKAGE_PREFIX}-expat"
         "${MINGW_PACKAGE_PREFIX}-natron_openexr"
         "${MINGW_PACKAGE_PREFIX}-pystring")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             "${MINGW_PACKAGE_PREFIX}-pkg-config"
             "${MINGW_PACKAGE_PREFIX}-cc"
             "git")
options=('strip')
source=("${_realname}"::"git+https://github.com/imageworks/OpenColorIO.git#commit=${gitcommit}"
        OpenColorIO.pc
        ocio-2.1.1.patch)
sha256sums=('SKIP'
            '1e825196e03f26f30d426f84c01ad408ab6cbaaae5b7a06faa7571c2733d6936'
            '8047d94b5b940c9686b2df34f441ff53947877bcdaae82072180d68c9d547138')

pkgver() {
  cd "${srcdir}/${_realname}"
  printf "%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
  cd ${_realname}
  #patch -p1 -i ${srcdir}/ocio-2.1.1.patch
}

build() {
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p build-${MINGW_CHOST} && cd build-${MINGW_CHOST}

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DOCIO_BUILD_APPS=ON \
    -DOCIO_BUILD_NUKE=OFF \
    -DOCIO_BUILD_DOCS=OFF \
    -DOCIO_BUILD_TESTS=OFF \
    -DOCIO_BUILD_GPU_TESTS=OFF \
    -DOCIO_BUILD_PYTHON=OFF \
    -DOCIO_USE_OPENEXR_HALF=OFF \
    -DCMAKE_BUILD_TYPE=Release \
    -DCMAKE_CXX_STANDARD=14 \
    -DCMAKE_VISIBILITY_INLINES_HIDDEN=ON \
    -DCMAKE_CXX_VISIBILITY_PRESET=hidden \
    -DOCIO_INLINES_HIDDEN=ON \
    ../${_realname}
  make
}

package() {
  cd build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  mkdir -p ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig
  cp ${srcdir}/OpenColorIO.pc ${pkgdir}${MINGW_PREFIX}/lib/pkgconfig/
}
