# Maintainer: Alexey Pavlov <alexpux@gmail.com>

_realname=openimageio17
pkgbase=mingw-w64-${_realname}
pkgname="${MINGW_PACKAGE_PREFIX}-${_realname}"
pkgver=1.7.19
pkgrel=1
pkgdesc="A library for reading and writing images, including classes, utilities, and applications (mingw-w64)"
arch=('any')
url="http://www.openimageio.org/"
license=("custom")
conflicts=("${MINGW_PACKAGE_PREFIX}-openimageio")
depends=("${MINGW_PACKAGE_PREFIX}-boost"
         "${MINGW_PACKAGE_PREFIX}-freetype"
         "${MINGW_PACKAGE_PREFIX}-jasper"
         "${MINGW_PACKAGE_PREFIX}-giflib"
         "${MINGW_PACKAGE_PREFIX}-glew"
         "${MINGW_PACKAGE_PREFIX}-libjpeg"
         "${MINGW_PACKAGE_PREFIX}-libpng"
         "${MINGW_PACKAGE_PREFIX}-LibRaw-gpl2"
         "${MINGW_PACKAGE_PREFIX}-libwebp"
         "${MINGW_PACKAGE_PREFIX}-libtiff"
         "${MINGW_PACKAGE_PREFIX}-opencolorio"
         "${MINGW_PACKAGE_PREFIX}-openexr"
         "${MINGW_PACKAGE_PREFIX}-openjpeg"
         "${MINGW_PACKAGE_PREFIX}-zlib")
makedepends=("${MINGW_PACKAGE_PREFIX}-cmake"
             #"${MINGW_PACKAGE_PREFIX}-qt5"
             "${MINGW_PACKAGE_PREFIX}-python2"
             "${MINGW_PACKAGE_PREFIX}-boost")
optdepends=(#"${MINGW_PACKAGE_PREFIX}-qt4: iv image viewer"
            "${MINGW_PACKAGE_PREFIX}-python2: bindings support")
options=('strip' 'buildflags')
source=(${_realname}-${pkgver}.tar.gz::https://github.com/OpenImageIO/oiio/archive/Release-${pkgver}.tar.gz
        fix-mingw-w64.patch
        workaround-ansidecl-h-PTR-define-conflict.patch
        0001-MinGW-w64-include-winbase-h-early-for-TCHAR-types.patch
        0003-boost-latest.patch
        0004-guard-msvc-stuff.patch
        0005-fix-initialization.patch
        oiio-1.5.23-invalidatespec.patch
        oiio-1.7.10-c99math.patch
        oiio-1.7.10-nothreads.patch
        oiio-1.7.12-tiffposition.patch
	oiio-1.7.17-highlightmode.patch
	oiio-1.7.17-tifftiles.patch)
sha256sums=('skip'
            '138b21e2c21fc43023f5788fa107f904454fda7812d1ab4f762de18b323b8649'
            'ab6bde02c0c05495a9cb13441535d42f4d5929bd930859d75f589d12be9a182b'
            'b7cc78d0f535ceb1aba68b11970c32ffbf04815d229b12e64e2c2c0b3403b4b2'
            'ac7039e5e9ba7e1a4e92d5f674282d270d0dee7afd96d3899f12544a0f6392d1'
            '3a73e95486a5a50685eed753f11c433bcf9f1b1a46947d6ae1350c8f68568536'
            'bddb6c328e13d7b53e511acba4e21cb6a3b2a09a961c34a566bf38c84622fed4'
            'skip'
            'skip'
            'skip'
            'skip'
            'skip'
            'skip'
	    'skip'
	    'skip')

prepare() {
  cd ${srcdir}/oiio-Release-${pkgver}
  patch -p1 -i ${srcdir}/fix-mingw-w64.patch
  patch -p1 -i ${srcdir}/workaround-ansidecl-h-PTR-define-conflict.patch
  patch -p1 -i ${srcdir}/0001-MinGW-w64-include-winbase-h-early-for-TCHAR-types.patch
  patch -p1 -i ${srcdir}/0003-boost-latest.patch
  patch -p1 -i ${srcdir}/0004-guard-msvc-stuff.patch
  patch -p1 -i ${srcdir}/0005-fix-initialization.patch
  patch -p1 -i ${srcdir}/oiio-1.5.23-invalidatespec.patch
  patch -p1 -i ${srcdir}/oiio-1.7.10-nothreads.patch
  patch -p1 -i ${srcdir}/oiio-1.7.10-c99math.patch
  patch -p1 -i ${srcdir}/oiio-1.7.12-tiffposition.patch
  patch -p1 -i ${srcdir}/oiio-1.7.17-highlightmode.patch
}

build() {
  export OPENJPEG_HOME=${MINGW_PREFIX}
  [[ -d "build-${MINGW_CHOST}" ]] && rm -rf "build-${MINGW_CHOST}"
  mkdir -p ${srcdir}/build-${MINGW_CHOST}
  cd ${srcdir}/build-${MINGW_CHOST}

  CXXFLAGS+=" -faligned-new"

  MSYS2_ARG_CONV_EXCL="-DCMAKE_INSTALL_PREFIX=" \
  ${MINGW_PREFIX}/bin/cmake.exe \
    -G"MSYS Makefiles" \
    -DCMAKE_INSTALL_PREFIX=${MINGW_PREFIX} \
    -DPYLIB_INSTALL_DIR=lib/python2.7/site-packages \
    -DUSE_QT=OFF \
    -DUSE_FIELD3D=OFF \
    -DUSE_FFMPEG=OFF \
    -DUSE_OPENJPEG=ON \
    -DUSE_OPENCV=OFF \
    -DUSE_OPENSSL=OFF \
    -DUSE_FREETYPE=ON \
    -DUSE_GIF=ON \
    -DUSE_LIBRAW=ON \
    -DUSE_NUKE=OFF \
    -DSTOP_ON_WARNING=OFF \
    -DOPENEXR_HOME=${MINGW_PREFIX} \
    -DILMBASE_HOME=${MINGW_PREFIX} \
    -DBOOST_ROOT=${MINGW_PREFIX} \
    -DTHIRD_PARTY_TOOLS_HOME=${MINGW_PREFIX} \
    -DFREETYPE_INCLUDE_PATH=${MINGW_PREFIX}/include/freetype2 \
    -DOPENJPEG_HOME=${MINGW_PREFIX} \
    -DOPENJPEG_INCLUDE_DIR=`pkg-config --variable=includedir libopenjp2` \
    -DLIBRAW_PATH=${MINGW_PREFIX}/libraw-gpl2 \
    -DCMAKE_SHARED_LINKER_FLAGS=" -Wl,--export-all-symbols -Wl,--enable-auto-import " \
    -DOIIO_BUILD_TESTS=ON \
    ../oiio-Release-${pkgver}

  make
}

package() {
  cd "${srcdir}"/build-${MINGW_CHOST}
  make DESTDIR=${pkgdir} install
  install -Dm644 ${srcdir}/oiio-Release-${pkgver}/LICENSE "${pkgdir}${MINGW_PREFIX}/share/licenses/${_realname}/LICENSE"
}
